
apply plugin: 'java'
//apply plugin: 'maven'
apply plugin: 'project-report'

group = 'fr.fbe.mower'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
    mavenCentral()
}

configurations {
    integTestCompile.extendsFrom runtime
    integTestRuntime.extendsFrom integTestCompile
}

dependencies {

    integTestCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'junit', name: 'junit', version:'4.11'

    integTestCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'

    integTestCompile group: 'org.powermock', name: 'powermock-module-junit4', version:'1.5.5'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version:'1.5.5'

    integTestCompile group: 'org.powermock', name: 'powermock-api-mockito', version:'1.5.5'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version:'1.5.5'

    integTestCompile group: 'org.easytesting', name: 'fest-assert', version:'1.4'
    testCompile group: 'org.easytesting', name: 'fest-assert', version:'1.4'

    compile group: 'org.projectlombok', name: 'lombok', version:'1.12.6'

    integTestCompile files("$project.buildDir/classes/main")

}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
        }

    test {
        java.srcDir file('src/test/java')
        resources.srcDir file('src/test/resources')
    }

    integTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
    }
}



task integTest(type: Test, dependsOn: assemble) {
    description = "run integration tests"
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    
    beforeTest { descriptor ->
        logger.lifecycle("Running IT : " + descriptor)
    }

    reports.junitXml.destination = file("${project.buildDir}/it-reports")
    reports.html.destination = file("${project.buildDir}/it-reports")

}

check.dependsOn integTest


test {
    beforeTest { descriptor ->
        logger.lifecycle("Running UT : " + descriptor)
    }
    reports.junitXml.destination = file("${project.buildDir}/ut-reports")
    reports.html.destination = file("${project.buildDir}/ut-reports")
}

tasks.withType(Test) {
    testLogging.showStandardStreams = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}


